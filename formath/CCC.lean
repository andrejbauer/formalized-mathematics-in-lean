import Mathlib.CategoryTheory.Closed.Cartesian

/-! We demonstrate how to use Lean's syntactic cappabilites to implement the internal
    language of a cartesian closed category.
-/

open CategoryTheory
open MonoidalCategory

section InternalLangage
  open Lean

  /- We first define the custom syntax. We shall use the following syntactic categories:
    * `entry` for an entries in the typing contexts
    * `context` for typing contexts
    * `cccterm` for the terms of the internal language, called Î»-terms
  -/

  /-- A context entry -/
  declare_syntax_cat entry
  syntax ident ":" term : entry

  /-- A context takes the form `xâ‚ : Xâ‚, â€¦, xâ‚™ : Xâ‚™` where `xáµ¢` are identifiers and `Xáµ¢` objects of a category. -/
  declare_syntax_cat context
  syntax entry,* : context

  /-- The syntax of Î»-terms -/
  declare_syntax_cat cccterm

  /-- a variable is a term -/
  syntax ident : cccterm

  /-- the unique (generalized) element of the terminal object -/
  syntax "tt" : cccterm

  /-- ordered pair -/
  syntax "âŸ¨" cccterm "," cccterm "âŸ©" : cccterm

  /-- first projection -/
  syntax "fst" cccterm : cccterm

  /-- second projection -/
  syntax "snd" cccterm : cccterm

  /-- Î»-abstraction -/
  syntax "Î» " ident " : " term ", " cccterm : cccterm

  /-- Parentheses -/
  syntax "(" cccterm ")" : cccterm

  /-- morphism application -/
  syntax "$(" term ")" cccterm : cccterm

  /-- a Î»-term in a context -/
  syntax context "âŠ¢" cccterm : term

  /-- Convert a context `xâ‚ : Xâ‚, â€¦, xâ‚™ : Xâ‚™` to the term `Xâ‚ âŠ— â‹¯ âŠ— Xâ‚™`,
      making sure that the empty context is the terminal object `ğŸ™_ _` and
      that `x : X` is just `X`, rather than `X âŠ— ğŸ™_ ğ’`.
  -/
  partial def prodify : TSyntax `context â†’ MacroM Term
  | `(context| ) => `(ğŸ™_ _)
  | `(context| $_:ident : $A:term) => `($A)
  | `(context| $_:ident : $A:term, $Î“:entry,*) =>
    do
      let Î“ â† `(context| $Î“:entry,*)
      let As â† prodify Î“
      `($A âŠ— $As)
  | _ => Macro.throwError "invalid context syntax"

  /-- Given an identifier `x` and a context `Î“`, compute the projection from `Î“` determined by `x`. -/
  partial def project (x : Name) : TSyntax `context â†’ MacroM Term
  | `(context| ) => Macro.throwError s!"unkown identifier {x}"
  | `(context| $y:ident : $A:term) =>
      -- the only thing that can be projected is `x` by the identity morphism
      if x = y.getId then `(ğŸ™ $A) else Macro.throwError s!"unkown identifier {x}"
  | `(context| $y:ident : $A:term, $Î“:entry,*) =>
    if x = y.getId then
      `(ChosenFiniteProducts.fst $A _)
    else do
      let Î“ â† `(context| $Î“:entry,*)
      let p â† project x Î“
      `(ChosenFiniteProducts.snd $A _ â‰« $p)
  | _ => Macro.throwError "invalid context syntax"

  /-- Conversion of the internal syntax to a (term representing) morphism -/
  macro_rules
  | `($Î“:context âŠ¢ $x:ident) => project x.getId Î“
  | `($Î“:context âŠ¢ tt) =>
    /- We could skip using `prodify` here and just return `(ChosenFiniteProducts.toUnit _)`, but the
       result is a bit too polymorphic, as `âŠ¢ tt` would denote *any* morphihm `toUnit X`. -/
    do { let A â† prodify Î“ ; `(ChosenFiniteProducts.toUnit $A) }
  | `($Î“:context âŠ¢ âŸ¨ $a:cccterm, $b:cccterm âŸ©) => `(ChosenFiniteProducts.lift ($Î“:context âŠ¢ $a) ($Î“:context âŠ¢ $b))
  | `($Î“:context âŠ¢ fst $a:cccterm) => `(($Î“:context âŠ¢ $a) â‰« ChosenFiniteProducts.fst _ _)
  | `($Î“:context âŠ¢ snd $a:cccterm) => `(($Î“:context âŠ¢ $a) â‰« ChosenFiniteProducts.snd _ _)
  | `($Î“:entry,* âŠ¢ Î» $x:ident : $A:term , $a:cccterm) => `(CartesianClosed.curry ($x:ident : $A:term, $Î“:entry,* âŠ¢ $a))
  | `($Î“:context âŠ¢ $($f:term) $a:cccterm) => `(($Î“:context âŠ¢ $a) â‰« $f)
  | `($Î“:context âŠ¢ ($a:cccterm)) => `($Î“:context âŠ¢ $a)

end InternalLangage

section Examples

  /- We work over a a category with (chosen) finite products. -/
  universe u v
  variable {ğ’ : Type u} [Category.{v, u} ğ’] [fp : ChosenFiniteProducts ğ’] [ccc : CartesianClosed ğ’]

  open ChosenFiniteProducts

  /-- the identity map -/
  example {X : ğ’} : X âŸ¶ X := x : X âŠ¢ x

  /-- the twist morphism -/
  example {X Y : ğ’} : X âŠ— Y âŸ¶ Y âŠ— X :=
    x : X, y : Y âŠ¢ âŸ¨ y, x âŸ©

  /-- the diagonal -/
  example {X : ğ’} : X âŸ¶ X âŠ— X :=
  x : X âŠ¢ âŸ¨ x, x âŸ©

  /-- the constant map -/
  example {X Y : ğ’} : X âŸ¶ (Y âŸ¹ X) := x : X âŠ¢ Î» y : Y, x

  /-- the first projection is the first projection -/
  example {X Y : ğ’} : (p : X âŠ— Y âŠ¢ fst p) = (p : X âŠ— Y âŠ¢ $(fp.fst X Y) p) := by simp

  /-- A silly example showing that we can embed the internal language inside `$(â‹¯)`. Please don't do this. -/
  example {X : ğ’} : X âŸ¶ X := x : X âŠ¢ $(y : X âŠ¢ y) x

  /-- identity on the terminal -/
  example : ğŸ™_ ğ’ âŸ¶ ğŸ™_ ğ’ := âŠ¢ tt

  /-- composition of morphisms -/
  example {X Y Z: ğ’} (g : Y âŸ¶ Z) (f : X âŸ¶ Y): X âŸ¶ Z :=
    x : X âŠ¢ $(g) $(f) x

  /-- right associator -/
  def assocRight (X Y Z : ğ’) : (X âŠ— Y) âŠ— Z âŸ¶ X âŠ— (Y âŠ— Z) :=
  p : (X âŠ— Y) âŠ— Z âŠ¢ âŸ¨fst (fst p), âŸ¨snd (fst p), snd pâŸ©âŸ©

  /-- left associator -/
  def assocLeft (X Y Z : ğ’) : X âŠ— (Y âŠ— Z) âŸ¶ (X âŠ— Y) âŠ— Z :=
  p : X âŠ— (Y âŠ— Z) âŠ¢ âŸ¨âŸ¨fst p, fst (snd p)âŸ©, snd (snd p)âŸ©

  /-- the associators are inverses -/
  example {X Y Z : ğ’} : assocLeft X Y Z â‰« assocRight X Y Z = ğŸ™ _ := by
   simp [assocLeft, assocRight]
   aesop_cat

end Examples
